#!/usr/bin/env objj

@import <OJUnit/OJTestRunnerText.j>
@import <OJCov/OJCoverageRunnerText.j>

CPLogRegister(CPLogPrint, "warn");

var parser = new (require("args").Parser)();

parser.usage("INPUT_FILE ...");
parser.help("Runs the given suite of tests.");

parser.option("-c", "coverage")
    .def(false)
    .set(true)
    .help("Runs test coverage along with your OJUnit tests.");

parser.option("-t", "threshold")
    .def(0.8)
    .set()
    .help("Set the threshold for test coverage to the specified percentage. Implies -c.");
    
parser.option("--no-accessors", "no_accessors")
    .def(false)
    .set(true)
    .help("Disable testing accessors for coverage testing. Implies -c.");

parser.helpful();

function main(args) {
    var options = parser.parse(args);
    
    if (options.args.length < 1)
    {
        parser.printUsage(options);
        return;
    }

    if (options.coverage || options.threshold || options.no_accessors)
    {
        var threshold = options.threshold > 1 ? options.threshold / 100 : options.threshold;

        var runner = [[OJCoverageRunnerText alloc] initWithThreshold:threshold testAccessors:!options.no_accessors];
    }
    else
    {
        var runner = [[OJTestRunnerText alloc] init];
    }
    
    [runner startWithArguments:options.args];
}
